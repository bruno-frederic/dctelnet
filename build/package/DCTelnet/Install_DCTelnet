; $VER: DCTelnet Installscript 1.8-dev (xx.2.26)

(SET @app-name "DCTelnet")
(SET @default-dest "SYS:")


;---[ Start installation ]----------------------------------------------------

(message "\n\n--××·· -÷- ··××---÷---××·· -÷- ··××---÷---××·· -÷- ··××-- -÷-\n\n"
		 "DCTelnet - A classic Amiga Telnet/BBS client with Zmodem\n\n"
		 "--××·· -÷- ··××---÷---××·· -÷- ··××---÷---××·· -÷- ··××-- -÷-")


(set #os-vernum (getversion "libs:version.library"))
(set #os-ver (/ #os-vernum 65536))

; Needed for GlowIcons
(set #iconlib-vernum (getversion "libs:icon.library"))
(set #iconlib-ver (/ #iconlib-vernum 65536))


(transcript "Running on OS v" #os-ver)

(if (< #os-ver 36)
	(
		(message "Please note that this program requires AmigaOS 2.00+!\n\n"
					"It will NOT work on 1.x.")
	)
)


(set #reqtools-ver (getversion "libs:reqtools.library"))

(if (= #reqtools-ver 0)
	(
		(if (< #os-ver 37)
			(
				(message "Please note that this program requires ReqTools library!\n"
					"You will need to install it before running DCTelnet.\n\n"
					"On AmigaOS / Kickstart 2.00 & 2.02,\n"
					"ReqTools v2.9a from Aminet fails to load.\n"
					"An older version of ReqTools is required:\n"
					"ReqTools-2.2-user.lha\n\n"
					"available here:\n\n"
					"https://www.nic.funet.fi/pub/amiga/system/libraries/")
			)
			; else
			(
				(message "Please note that this program requires ReqTools library!\n\n"
					"You will need to install it before running DCTelnet.\n\n"
					"It can be downloaded from Aminet: util/libs/ReqToolsUsr")
			)
		)





	)
)



;---[ Start installation ]----------------------------------------------------


(SET DestDir
	(ASKDIR	(prompt "Please select where you would like DCTelnet to be installed\n"
					"a drawer will be created")
		(help @askdir-help)
		(default @default-dest)
		(disk)
     )
)

(SET DestDir	(TACKON DestDir "DCTelnet"))
(SET @default-dest DestDir)
(SET DocsDir	(TACKON DestDir "Docs"))
(SET SCDir	(TACKON DestDir "SCIcons"))
(SET WBDir	(TACKON DestDir "WBIcons"))

	(COPYFILES
		(prompt	"\n\nInstalling DCTelnet executable for 68 000.\n")
		(help @copyfiles-help)
		(source "DCTelnet")
		(dest	 DestDir)
		(infos)
		(confirm)  ; if the "confirm" option is present, user will be prompted to indicate which
		           ; files are to be copied, else the files will be copied silently.
	)
	(COPYFILES
		(prompt	"\n\nInstalling DCTelnet executable for 68 020 CPU.\n")
		(help @copyfiles-help)
		(source "DCTelnet.020")
		(dest	DestDir)
		(infos)
		(confirm)
	)

	(COPYFILES
		(prompt	"Installing the DCTelnet documentation.")
		(help @copyfiles-help)
		(source "docs")
		(dest	 DocsDir)
		(infos)
		(confirm)
		(all)			; needed to copy whole directory
	)

	(if (< #iconlib-ver 45)
		(
			(set #glowIconsDest	(TACKON DestDir "GlowIcons"))
		)
		;else
		(
			(set #glowIconsDest DestDir)
		)
	)

	(COPYFILES
		(prompt	"Installing optional GlowIcons set.")
		(help @copyfiles-help)
		(source "GlowIcons")
		(dest	 #glowIconsDest)
		(infos)
		(confirm "AVERAGE")	; so even Average user show this confirm step
		(all)
	)

(SET #address-book-path	(TACKON DestDir "DCTelnet.book"))
(if (<> (exists #address-book-path) 0)
	(
		(message "\nWARNING: An address book already exists at the destination.\n\n"
				"The existing file will be renamed to 'DCTelnet-Backup.Book'.\n")

		(COPYFILES
			(prompt	"Backing up address book.")
			(help @copyfiles-help)
			(source #address-book-path)
			(dest	 DestDir)
			(newname "DCTelnet-Backup.Book")
			(infos)
		)

	)
)

	(COPYFILES
		(prompt	"Installing starter address book.")
		(help @copyfiles-help)
		(source "DCTelnet.Book")
		(dest	 DestDir)
		(infos)
	)

	(COPYFILES
		(prompt	"Installing the DCTelnet SCIcons (ANSI) icons.")
		(help	"\n\nAs from 1.4, DCTelnet now uses various icons to represent functions.\n\n"
			"If you use Workbench to run DCTelnet, it will default to MWB (WB) style icons,\n"
			"otherwise it will load and use the ANSI (SC) style icons.")
		(source "SCIcons")
		(dest	 SCDir)
		(all)
	)

	(COPYFILES
		(prompt	"Installing the DCTelnet WBIcons (MWB) icons.")
		(help	"\n\nAs from 1.4, DCTelnet now uses various icons to represent functions.\n\n"
			"If you use Workbench to run DCTelnet, it will default to MWB (WB) style icons,\n"
			"otherwise it will load and use the ANSI (SC) style icons.")
		(source "WBIcons")
		(dest	 WBDir)
		(all)
	)

	; COPYLIB: check the version to be installed and the destination version before copying
	(COPYLIB
		(prompt	"\n\nInstalling FTP support: xprftp.library")
		(help	"\n\nFTP is a hi-speed and far more efficient transfer method than ZModem\n\n"
			"Quite a number of Amiga BBS systems support this protocol.\n\n"
			"XPR-FTP Written By Zed/DC")
		(source "libs/xprftp.library")
		(dest	"libs:")
		(confirm)
	)

	(COPYLIB
		(prompt	"\n\nInstalling Zmodem support: xprzmodem.library")
		(help	"\n\nZModem is used to transfer files via Telnet.\n\n"
			"If it is not already installed, it is recommended to do so."
			)
		(source "libs/xprzmodem.library")
		(dest	"libs:")
		(confirm)
	)

	(COPYLIB
		(prompt	"\n\nInstalling external terminal emulation support: xemvt340.library")
		(help	"\n\nExternal VT emulation support.\n\n"
			"If it is not already installed, it is recommended to do so."
			)
		(source "libs/xemvt340.library")
		(dest	"libs:")
		(confirm)
	)

	(COPYLIB
		(prompt	"\n\nInstalling device for ANSI color display: ibmcom.device")
		(help	"\n\nibmcon.device is used to display the 'pretty' ANSI gfx/colors.\n\n"
			"It is essential that this be installed on your system for DCTelnet."
			)
		(source "devs/ibmcon.device")
		(dest	"devs:")
		(confirm)
	)

	(COPYFILES
		(prompt	"Installing a few nice ANSI/BBS fonts.")
		(help	"\n\nCheck which fonts you would like to install on your system.\n\n"
			"These 3 are some of the most popular and nicest BBSing fonts."
		)
		(source "fonts")
		(dest	"fonts:")
		(fonts)
		(confirm)
		(all)
	)

(message "\n\nDCTelnet is now installed\n\n"
		"For bugreports/support etc. please refer to the included documentation...\n\n"
		"...ENJOY!!!")

(EXIT)
