/*
This script builds DCTelnet with SAS/C under Amiga Shell 3.2
dual-target support for 68000 and 68020 CPU

Make this script executable :
    PROTECT #?.arexx +SRE

Must be run from "build/" dir

It captures the output to Build-output.txt

Build on RAM: volume to speedup
*/

/* Instruct ARexx to send commands to the Amiga shell */
ADDRESS COMMAND

initialDir = PRAGMA(DIRECTORY, "/")
repoRootDir = PRAGMA(DIRECTORY)


/* || concatenate strings without spaces */
outFile  = initialDir  || "/Build-output.txt"
srcDir   = repoRootDir || "/src"
docsDir  = repoRootDir || "/docs"


'ECHO "Building starts at: " NOLINE >' outFile
'DATE >>' outFile

'ASSIGN Include: "'srcDir'/third_party/AmiTCP/netinclude"'
'ASSIGN Include: "'srcDir'/third_party/ReqTools/include"    ADD'
'ASSIGN Include: "'srcDir'/third_party/Xem/INCLUDE"         ADD'
'ASSIGN Include: "'srcDir'/third_party/XPRotocol/library"   ADD'
'ASSIGN Include: SC:include                                 ADD'


/* Specifies a new stack value for your current ARexx program */
dummy=PRAGMA( "STACK", 150000 )

cpu=000
buildDir="RAM:build-" || cpu
'echo -------------- Building in 'buildDir' -------------- >>' outFile
'COPY FROM' srcDir 'TO' buildDir 'CLONE QUIET'
dummy=PRAGMA(DIRECTORY, buildDir)
'smake CPU='cpu' >>' outFile

cpu=020
buildDir="RAM:build-" || cpu
'echo -------------- Building in 'buildDir' -------------- >>' outFile
'COPY FROM' srcDir 'TO' buildDir 'CLONE QUIET'
dummy=PRAGMA(DIRECTORY, buildDir)
'smake CPU='cpu' >>' outFile


'ECHO "Compile complete at: " NOLINE >>' outFile
'DATE >>' outFile


IF EXISTS('RAM:build-000/DCTelnet') | EXISTS('RAM:build-020/DCTelnet')
THEN DO
    /* EXISTS() does not support wildcard in directory name*/

    SAY ""
    SAY "Built executable files:"
    'LIST   RAM:build-0??/DCTelnet'
    'md5sum RAM:build-0??/DCTelnet'

    SAY ""
    SAY "Build the LHA package:"
    'DELETE RAM:build-package  ALL >>' outFile
    'COPY  FROM "'initialDir'/package"         TO RAM:build-package CLONE ALL  >>' outFile
    dummy=PRAGMA(DIRECTORY, "RAM:build-package")

    'COPY  FROM RAM:build-000/DCTelnet         TO DCTelnet/DCTelnet        CLONE >>' outFile
    'COPY  FROM RAM:build-020/DCTelnet         TO DCTelnet/DCTelnet.020    CLONE >>' outFile

    'COPY  FROM "'repoRootDir'/LICENSE"         TO DCTelnet/docs            CLONE  >>' outFile
    'COPY  FROM "'docsDir'/DCTelnet.#?"        TO DCTelnet/docs            CLONE  >>' outFile
    'COPY  FROM "'docsDir'/FILE_ID.DIZ"        TO ""                       CLONE  >>' outFile


    'DELETE .info  DCTelnet/#?/.info >>' outFile

    archiveName = initialDir || '/DCTelnet.lha'
    /* Warning: removing the files before on Windows may cause issues with these commands */
    'DELETE "'archiveName'" >>' outFile
    'lha -r -a -Qa a "'archiveName'" >>' outFile
    /*
        -r  Collect files recursively
        -a  Preserve file attributes (D)
        -Qa Use simple console I/O
    */
    'md5sum "'archiveName'"'
END

/* Normal end */
Exit
